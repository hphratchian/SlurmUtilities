#!/usr/bin/perl
#
# This script is used to submit Gaussian calculations on MERCED.
#
# When using this script the number of processors and the amount of memory
# to be used by Gaussian should NOT be defined in the Gaussian input file.
# Instead, the user should define these values as command line option
# switches to this script.
#
# The full set of available switches can be seen by running perldoc on this
# script, which will interpret the POD block that follows these comments.
# Specifically, run the following at the command line:
#       perldoc gbatch
#
#
# Hrant P. Hratchian
# University of California, Merced
# hhratchian@ucmerced.edu
#
#
#
#   Set-up communication with modules.
#
    use Cwd;
#
#   Get the executing username and then set option defaults, including some
#   that may be user-dependent.
#
    $username = getpwuid( $< );
    chomp($username);
    $do_submission  = 1;
    $run_in_scratch = 0;
    $queue_flag     = "std.q";
    $mem_amount     = "96GB";
    $nproc          = 0;
    $nproc_full     = 20;
    $nproc_std      = 20;
    $nproc_fast     = 20;
    $nproc_long     = 24;
    $nlinda         = 0;
    $walltime       = "01:00:00";
    $g_type         = "gdv";
    $g_module       = "gdv";
#
#   Load option switches and file names from the command line. Then, set
#   the .sbatch file name.
#
    foreach $temp (@ARGV){
      chomp($temp);
#
#     General program options...
      if($temp =~ /^-h$/i){
        exec("perldoc","gbatch");
        exit 0;
      }elsif($temp =~ /^-v$/i){
        print "\ngbatch for MERCED.\n";
        print "Last modified by HPH on June 18, 2015.\n\n";
        exit 0;
#
#     Gaussian job setting controls...
      }elsif($temp =~ /^-mem=(\d+[a-zA-Z][a-zA-Z])$/i){
        $mem_amount = $1;
      }elsif($temp =~ /^-nproc=(\d+)$/i){
        $nproc=$1;
      }elsif($temp =~ /^-nlinda=(\d+)$/i){
        $nlinda=$1;
        die "\ngbatch does not yet allow Gaussian calculations employing Linda.\n\n";
      }elsif($temp =~ /^-time=(\S+)$/i){
        $walltime = $1;
      }elsif($temp =~ /^-gdv$/i){
        $g_type = "gdv";
        $g_module = "gdv";
      }elsif($temp =~ /^-gt$/i){
        $g_type = "gdv -exedir=\$HOME/gauss:\$HOME/gauss/exe-dir:\$GAUSS_EXEDIR";
        $g_module = "gdv";
      }elsif($temp =~ /^-gdvi02\+$/i){
        $g_type = "gdv";
        $g_module = "gdv-20150105-i02+";
      }elsif($temp =~ /^-gti02\+$/i){
        $g_type = "gdv -exedir=\$HOME/gauss:\$HOME/gauss/exe-dir:\$GAUSS_EXEDIR";
        $g_module = "gdv-20150105-i02+";
      }elsif($temp =~ /^-gdvi03\+$/i){
        $g_type = "gdv";
        $g_module = "gdv-20150518-i03+";
      }elsif($temp =~ /^-gti03\+$/i){
        $g_type = "gdv -exedir=\$HOME/gauss:\$HOME/gauss/exe-dir:\$GAUSS_EXEDIR";
        $g_module = "gdv-20150518-i03+";
      }elsif($temp =~ /^-gdvi06$/i){
        $g_type = "gdv";
        $g_module = "gdv-20160512-i06";
      }elsif($temp =~ /^-gti06$/i){
        $g_type = "gdv -exedir=\$HOME/gauss:\$HOME/gauss/exe-dir:\$GAUSS_EXEDIR";
        $g_module = "gdv-20160512-i06";
      }elsif($temp =~ /^-gdvi09$/i){
        $g_type = "gdv";
        $g_module = "gdv-20160804-i09";
      }elsif($temp =~ /^-gti09$/i){
        $g_type = "gdv -exedir=\$HOME/gauss:\$HOME/gauss/exe-dir:\$GAUSS_EXEDIR";
        $g_module = "gdv-20160804-i09";
      }elsif($temp =~ /^-gdvi10\+$/i){
        $g_type = "gdv";
        $g_module = "gdv-20170407-i10+";
      }elsif($temp =~ /^-gti10\+$/i){
        $g_type = "gdv -exedir=\$HOME/gauss:\$HOME/gauss/exe-dir:\$GAUSS_EXEDIR";
        $g_module = "gdv-20170407-i10+";
      }elsif($temp =~ /^-g16$/i){
        $g_type = "g16";
        $g_module = "g16";
      }elsif($temp =~ /^-g09$/i){
        $g_type = "g09";
        $g_module = "g09";
      }elsif($temp =~ /^-g09b01$/i){
        $g_type = "g09";
        $g_module = "g09-b01";
      }elsif($temp =~ /^-g09d01$/i){
        $g_type = "g09";
        $g_module = "g09-d01";
#
#     Submission & queue controls...
      }elsif($temp =~ /^-sub$/i){
        $do_submission = 1;
      }elsif($temp =~ /^-nosub$/i){
        $do_submission = 0;
      }elsif($temp =~ /^-fullq$/i){
        $queue_flag = "full.q";
      }elsif($temp =~ /^-stdq$/i){
        $queue_flag = "std.q";
      }elsif($temp =~ /^-fastq$/i){
        $queue_flag = "fast.q";
      }elsif($temp =~ /^-longq$/i){
        $queue_flag = "long.q";
      }elsif($temp =~ /^-runscratch$/i){
        $run_in_scratch = 1;
      }elsif($temp =~ /^-norunscratch$/i){
        $run_in_scratch = 0;
      }elsif($temp =~ /^-name=([a-zA-Z]\S+)$/i){
        $job_name=$1;
#
#     Unknown switch...
      }elsif($temp =~ /^-(.*)/){
        die "\n\nUnknown switch sent to gbatch: $temp\n\n";
#
#     Input file found...
      }else{
        push(@filelist,$temp);
      }
    }
    if(@filelist < 1){die "\nNo GAUSSIAN input files given!\n\n"}
    $qsub_file = $filelist[0];
    unless($qsub_file =~ s/\.(?:gjf|com)/.sbatch/){
      print "\n\nNOTE: Appending .sbatch\n\n";
      $qsub_file .= ".sbatch";
    }
#
#   If the user hasn't specified nproc, set it to the queue-dependent
#   default value.
#
    unless($nproc){
      if($queue_flag eq "full.q"){
        $nproc = $nproc_full;
      }elsif($queue_flag eq "std.q"){
        $nproc = $nproc_std;
      }elsif($queue_flag eq "fast.q"){
        $nproc = $nproc_fast;
      }elsif($queue_flag eq "long.q"){
        $nproc = $nproc_long;
      }else{
        die "\n\nLogic error setting default nproc setting based on queue.\n\n";
      }
    }
#
#   Set the name of the .err file and the qstat job name.
#
    $qerr_file = $filelist[0];
    unless($qerr_file =~ s/\.(?:gjf|com)/.err/){
      print "\n\nNOTE: Appending .err\n\n";
      $qerr_file .= ".err";
    }
    unless($job_name){$job_name = "$filelist[0]";}
    print "sbatch JOB NAME is $job_name.\n";
#
#   Determine the memory setting to send to SGE.
#
    if($mem_amount =~ /\s*(\d+)mb/i){
      $mem_requirement = $1;
    }elsif($mem_amount =~ /\s*(\d+)gb/i){
      $mem_requirement = $1 * 1024;
    }else{
      die "The units for requested memory allocation are invalid. -mem=$mem_amount\n\n";
    }
#
#   Determine the number of slots and the parallel environment to request
#   from the scheduler.
#
    if($nlinda){
      $nslots = $nlinda*$nproc;
      if($nproc == 20){
        $pe_type = "linda-20smp";
      }elsif($nproc == 24){
        $pe_type = "linda-24smp";
      }else{
        die "\n\nInvalid number of SMPs ($nproc) requested with Linda workers.\n\n";
      }
    }else{
      $nslots = $nproc;
      $pe_type = "smp";
    }
#
#   Put together the list of Gaussian command line switches controlling
#   resource use.
#
    $g_resources = "-m=$mem_amount -p=$nproc";
    if($nlinda){
      $g_resources .= " -w=\$MY_LINDA_WORKERS";
    }
    if($g_module =~ m/g09-b01/){$g_resources = " "};
#
#   Process the list of input files and build the %working_directory hash,
#   which gives the working directory to use when submitting each Gaussian
#   job via remote_submit.
#
    chomp($my_cwd = cwd);
    $my_cwd =~ s/\/\//\//g;
    for ($i=0;$i<@filelist;$i++){
      $filelist[$i] =~ s/$my_cwd\///;
      if($filelist[$i] =~ /\//){
        die "\n\nInput files must be in the CWD.\n\n";
      }
    }
#
#   Loop through @filelist and process each job one-at-a-time. This loop
#   compiles a complete list of CHK, RWF, and @ files that must be moved to
#   the local node's disk and then moved back after the Gaussian work is
#   complete. This is also the block of code that ensures the input files
#   are compliant with our submission requirements.
#
    foreach $input_file (@filelist){
      chomp($input_file);
      @current_at = ();
      ($current_chk,$current_rwf,@current_at) =
        &gauss_input_file_check($input_file);
      push(@inputfiles,$input_file);
      @chkfiles_temp = split /,/, $current_chk;
      foreach $chkfile (@chkfiles_temp){
        $chkfiles_to_return{$chkfile} += 1;
        if(-e $chkfile){
          $chkfiles_to_send{$chkfile} += 1;
        }
      }
      @rwffiles_temp = split /,/, $current_rwf;
      foreach $rwffile (@rwffiles_temp){
        $rwffiles_to_return{$rwffile} += 1;
        if(-e $rwffile){
          $rwffiles_to_send{$rwffile} += 1;
        }
      }
      foreach $atfile (@current_at){
        unless(-e $atfile){die "\n\n@-file $atfile is missing!\n\n";}
        $atfiles_to_send{$atfile} += 1;
      }
      $log_file = $input_file;
      unless($log_file =~ s/\.(?:gjf|com)/.log/){
        $log_file .= ".log";
      }
#
#     Add this job to the command list of Gaussian executions.
      push(@gauss_run,"merced_node_print -d > $log_file && $g_type $g_resources < $input_file >>& $log_file && sed -i \'s/Dipole orientation:/Dipole oientation:/\' $log_file");
    }
#
#   Build two arrays that are used to handled file transfer to/from the
#   compute node's local disk. The first array, @sendfiles, lists all the
#   files that must be moved TO the local disk before Gaussian is executed.
#   The second array, @removefiles, lists all of the files that should be
#   removed from the temperary directory before copying all files back to
#   the submission working directory.
#
    @sendfiles = @inputfiles;
    @removefiles = @inputfiles;
    while(($key,$value)=each %chkfiles_to_send){
      push(@sendfiles,$key);
    }
    while(($key,$value)=each %rwffiles_to_send){
      push(@sendfiles,$key);
    }
    while(($key,$value)=each %atfiles_to_send){
      push(@sendfiles,$key);
      push(@removefiles,$key);
    }
#
#   Write the submission file...
#
    open  QSUBFILE,"> $qsub_file";
    print QSUBFILE "#! /bin/csh\n";
    print QSUBFILE "#SBATCH -J $job_name\n";
    print QSUBFILE "#SBATCH -o $job_name.qlog\n";
    print QSUBFILE "#SBATCH -p $queue_flag\n";
    print QSUBFILE "#SBATCH --mem=$mem_requirement\n";
    print QSUBFILE "#SBATCH --nodes=1\n";
    print QSUBFILE "#SBATCH --ntasks=$nproc\n";
#hph    print QSUBFILE "#SBATCH --exclusive\n";
    print QSUBFILE "#SBATCH --time=$walltime\n";
#
    print QSUBFILE "#\n";
    print QSUBFILE "# Set up the Gaussian environment and prevent core files.\n";
    print QSUBFILE "module load gaussian/$g_module\n";
    print QSUBFILE "limit coredumpsize 0\n";
    print QSUBFILE "#\n";
    print QSUBFILE "# Set the Gaussian scratch directory to the job-specific SCRATCH directory.\n";
    print QSUBFILE "set MY_SCRATCH=\"\$TMPDIR\"\n";
    print QSUBFILE "setenv GAUSS_SCRDIR \$MY_SCRATCH\n";
    if($run_in_scratch){
      print QSUBFILE "#\n";
      print QSUBFILE "# Copy files to the scratch directory and then move there.\n";
      foreach $temp (@sendfiles){
        print QSUBFILE "cp $temp \$MY_SCRATCH\/\n";
      }
      print QSUBFILE "cd \$MY_SCRATCH\n";
    }
    if($nlinda){
      print QSUBFILE "#\n";
      print QSUBFILE "# Set things up for Linda.\n";
      print QSUBFILE "setenv GAUSS_LFLAGS \'-opt \"Tsnet.Node.lindarsharg: ssh\"\'\n";
      print QSUBFILE "set MY_LINDA_WORKERS=`pre-linda \$PE_HOSTFILE $nproc`\n";
    }
    print QSUBFILE "#\n";
    print QSUBFILE "# Run Gaussian...\n";
    foreach $temp (@gauss_run){
      chomp($temp);
      print QSUBFILE "$temp\n";
    }
    if($run_in_scratch){
      print QSUBFILE "#\n";
      print QSUBFILE "# Remove files that we don't want to copy back to the original submission working directory.\n";
      foreach $temp (@removefiles){
        chomp($temp);
        print QSUBFILE "rm -f $temp\n";
      }
      print QSUBFILE "#\n";
      print QSUBFILE "# Return to the original working directory and copy back all of the Gaussian files.\n";
      print QSUBFILE "cd -\n";
      print QSUBFILE "cp -f \$MY_SCRATCH\/\* .\n";
    }
    close QSUBFILE;
#
#   Set-up command line to submit the job to SGE and then submit it...
#
    if($do_submission){
      $sys_cmd = "sbatch $qsub_file";
      exec "$sys_cmd";
      print "Job $job_name submitted to the queue.\n";
    }else{
      print "Job NOT submitted. SBATCH file $qsub_file has been prepared.\n";
    }


####################################################################
#
#   Subroutine gauss_input_file_check
#
####################################################################

    sub gauss_input_file_check{
#
#   This routine is used to read through a Gaussian input file to check for
#   compliance on the Blacklight. Also, this subroutine will return the
#   name(s) of CHK, RWF, and/or @-files.
#
#   If an unknown link0 command is found, this subroutine drops an error
#   message and the script dies.
#
#   As INPUT, this routine takes the name of the Gaussian input file.
#
#   As OUTPUT, this routine returns (in this order):
#       1.  Name of the checkpoint file (the value returned by this
#           function always includes the extension ".chk");
#       2.  Name of the read-write file; and (the value returned by this
#           function always includes the extension ".rwf");
#       3.  List of at-files.
#
#   Note that requests for Linda within the Gaussian input file are NOT
#   understood by this routine and are ignored.
#
      use strict;
      if(@_ != 1){
        die "\nWrong number of parameters sent to Routine gauss_input_file_check.\n\n";
      }
      my($infile) = @_;
      my($chkfile,$rwffile,@AtFiles);
      my($current_line,$temp,@chkfile_list,@rwffile_list);
#
#     Open and read through the Gaussian input file to ensure compliane of
#     the link0 command block and to load the names of CHK, RWF, and
#     @-files.
#
      open (INFILE, "$infile");
      while (<INFILE>) {
        $current_line = $_;
        chomp($current_line);
        if ($current_line =~ /^\s*\%chk\s*\=\s*(\S*.chk)\s*$/i){
          push(@chkfile_list,$1);
        }elsif ($current_line =~ /^\s*\%chk\s*\=\s*(\S*)\s*$/i){
          push(@chkfile_list,"$1.chk");
        }elsif ($current_line =~ /^\s*\%oldchk\s*\=\s*(\S*.chk)\s*$/i){
          push(@chkfile_list,$1);
        }elsif ($current_line =~ /^\s*\%oldchk\s*\=\s*(\S*)\s*$/i){
          push(@chkfile_list,"$1.chk");
        }elsif ($current_line =~ /^\s*\%rwf\s*\=\s*(\S*.rwf)\s*$/i){
          push(@rwffile_list,$1);
        }elsif ($current_line =~ /^\s*\%rwf\s*\=\s*(\S*)\s*$/i){
          push(@rwffile_list,"$1.rwf");
        }elsif ($current_line =~ /^\s*\%kjob .*$/i){
        }elsif ($current_line =~ /^\s*\%subst .*$/i){
        }elsif ($current_line =~ /^\s*\%nosave\s*$/i){
        }elsif ($current_line =~ /^\s*\@(\S+)\s*$/){
          $temp = $1;
          $temp =~ s/\/N\s*$//;
          push(@AtFiles,$temp);
          print "Found \@-file: -->$temp<--\n";
        }elsif ($current_line =~ /^\s*\%/){
          die "\n\nInvalid link0 line found.\n\t$current_line\n\n";
        }
      }
      close (INFILE);
#
#     Form ',' delimited lists from temparary lists formed above.
#
      $chkfile = join ",",@chkfile_list;
      $rwffile = join ",",@rwffile_list;
#
#     Return to the calling program.
#
      return ($chkfile,$rwffile,@AtFiles);
    }


###########################################################################
# POD block...
#
=head1 NAME

B<gbatch> - This script is used to submit Gaussian calculations on MERCED.
When using this script the number of processors and the amount of memory to
be used by Gaussian should NOT be defined in the Gaussian input file.
Instead, the user should define these values as command line option
switches to this script.

=head1 SYNOPSIS

B<gbatch> [I<option_list>] F<gaussian_input_files>

=head1 DESCRIPTION

This script takes one or more Gaussian input file names as input and
prepares a SLURM submission file to submit to MERCED's queue system. By
default, the job is then submitted to the queue. To supress submission, use
the -nosub option described below.

=head1 OPTIONS

=head2 General Program Options

=over

=item B<-h>

Print help information.

=item B<-v>

Print version and modification information.

=back


=head2 Gaussian Job Options

=over

=item B<-mem=F<N>>

This switch is used to set the memory requested by Gaussian to the value
F<N>. When specified with this switch, the memory amount specified should
include a unit label. For example, to request 32 GB of memory use
B<-mem=32GB>. The default value is determined by the script version.

=item B<-nproc=F<N>>

This switch is used to set the number of shared-memory processors requested
by Gaussian to the value B<F<N>>. The default value is determined by the
script version.

=item B<-nlinda=F<N>>

This switch is used to set the number of distributed-memory Linda workers
requested by Gaussian to the value B<F<N>>. The default value is determined
by the script version.

=item B<-gdv>

=item B<-gt>

=item B<-g16>

=item B<-g09>

These switches control whether Gdv, gt (Gdv with test code in
$HOME/gauss:$HOME/gauss/exe-dir:$GAUSS_EXEDIR), or G09 is used. Gdv is run
by default. Other switches are available to request specific older versions
of Gaussian. These switches include B<-g16>, B<-g09b01>, B<-gdvi02+>,
B<-gdvi03+>, B<-gdvi06>, B<-gdvi09>, and B<gdvi10+>.

=back


=head2 Submission & Queue Control

=over

=item B<-sub>

=item B<-nosub>

These switches enable (B<-sub>) or disable (B<-nosub>) submission of the
processed Gaussian jobs to the queue system. In both cases, a submission
file (*.sbatch) is generated. A typical reason for disabling queue submission
is for debugging or to add custom blocks of code to the submission script.
By default, submission is enabled.

=item B<-time=F<hh:mm:ss>>

This switch sets the hard walltime limit for the job. If not set, this
option defaults to 1 hour.

=item B<-runscratch>

=item B<-norunscratch>

These switches determine whether the job is run from within the local
compute node scratch directory (B<-runscratch>) or from the NFS mounted
/home/ partition (B<-norunscratch>). When a job is run in the local scratch
directory, all files necessary for the calculations to run are copied to
the scratch directory before running Gaussian and then copied back after
all of the jobs complete. In either case, Gaussian scratch files are kept
on the local scratch partition. By default, jobs are run in the local
scratch directory.

=item B<-stdq>

=item B<-fullq>

=item B<-fastq>

=item B<-longq>

These switches are used to assign the queue to which the processed Gaussian
jobs are submitted.

=over

1. The standard queue (known to the queue system as F<std.q> and requested
   with the switch B<-stdq>). This queue has a 24 hour wall clock limit.

2. The full queue (known to the queue system as F<full.q> and requested
   with the switch B<-fullq>). This queue has a 24 hour wall clock limit.

3. The fast queue (known to the queue system as F<fast.q> and requested
   with the switch B<-fastq>).

4. The long queue (known to the queue system as F<long.q> and requested
   with the switch B<-longq>).

=back

By default, jobs are submitted to the standard queue.

=item B<-name=F<A>>

This switch is used to specify the name label given in the qstat (and
related) tables while a job is active in the queue system. The name given
must begin with a letter.

=back


=head1 VERSION SPECIFIC DEFAULTS

Defaults specific to the B<MERCED> version of B<gbatch> include:

=over

=item B<-mem>

The default memory requested (per Linda worker) is 96 GB. This is
equivalent to using B<-mem=96GB>.

=item B<-nproc>

The default number of shared-memory processors is 20. This is equivalent to
using B<-nproc=20>.

=item B<-nlinda>

The default number of Linda workers is 0, i.e. Linda parallelism is NOT
requested.

=back


=head1 CURRENT VERSION

Version: B<gbatch> for B<F<MERCED>> submissions.

Latest Modification(s): August 20, 2019.


=head1 MODIFICATION HISTORY

=over

=item August 20, 2019

This version of the script now includes a switch for selecting the full queue.

=item July 15, 2019

This version of the script now includes a switch for selecting g16 (-g16).

=item June 27, 2019

This version of the script now includes walltime limit option
(-time=hh:mm:ss) and also removes the "--exclusive" flag from the SLURM
submission script.

=item October 4, 2018

This is the initial version of gbatch, which is based on the gsub script on
MERCED that was previously used to submit jobs to the Grid Engine
scheduler.

=back


=head1 AUTHOR

=over

=item Hrant P. Hratchian

=item University of California, Merced

=item Merced, CA 95348

=item hhratchian@ucmerced.edu

=back


=cut
###########################################################################
